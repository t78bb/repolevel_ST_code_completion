PROGRAM PLC_PRG
VAR
	//Instance of Realsubject but this one is Local to prove on a local and a global object
	localSubjectInstance : Realsubject;
	
	//Pointer to the local subject. Needed to instantiate its own Proxy. 
	ptrLocalSubject : POINTER TO Realsubject := ADR(localSubjectInstance);
	
	//Null pointer to the RealSubject class to verify that the other pointers are valid pointers and not null
	ptrNullRealSubject : POINTER TO Realsubject;
	
	//Pointer needed to create the Connection bridge "Proxy" between the client and the subject 
	ptrProxy : POINTER TO Proxy;
	
	//Variable to store the password set by the client to perform the connection
	iTempPasswordLocal : INT;
	
	//Variable to store the INT passed by the client to be SET on the subject
	iSetValueLocal : INT;

	//Boolean value to that gets toggled when the client send a SET request.
	//It is toggled again after the Proxy's response
	bWriteRequest : BOOL;
	
	//Boolean value to that gets toggled when the client send a GET request.
	//It is toggled again after the Proxy's response
	bReadRequest : BOOL;
	
	//Variable to store locally if the request was a Write or Read request.
	//TRUE for a Write request and FALSE for a Read request
	bReadWriteLocal : BOOL;
	
END_VAR
VAR_INPUT
	//Variable to store the response log from the Proxy on a SET request
	sTempSetLog : STRING := 'Waiting for a request';
	//Variable to store the response log from the Proxy on a GET request
	sTempGetLog : STRING := 'Waiting for a request';
	
	//Variable to store the LAST GET response value locally
	iGetValueLocal : INT;
END_VAR

//The Visualization element can send a GET/SET request with all its parameters.
//Once the request is received by the PLC_PRG, the PLC_PRG creates the Proxy object to delegate the request and subject's connection to it.
//The default password value for the request is: 1234. It is specified in the static field .iRealPassword of the Proxy class

//If a WRITE or SET request is received
IF	bWriteRequest
	THEN
	//We specify TRUE bReadWriteLocal because it is a SET request
	bReadWriteLocal := TRUE;
	//First we verify if the pointer to RealSubject to be used is a valid pointer
	IF ptrLocalSubject <> ptrNullRealSubject
		//If so, 
		THEN
		//The Proxy object is created with all its parameters passed by the PLC_PRG local variables.
		//As there is no "Method Overloading" options is CODESYS (Or at least the constructor cannot be overloaded) the constructor must receive and handle all the possible scenarios
		// .ptrToRealSubject is a Pointer to the RealSubject instance that we try to conncect to
		// .iTempPassword is the password passed by the client in the visualization element
		// .iSetValue is the value passed by the client to be SET on the RealSubject. If the request is a GET request, this value has no effect on the output of the connection.
		// .bReadWrite is the boolean value that specifies if the request is a SET or GET request
		ptrProxy := __NEW( Proxy( ptrToRealSubject := ptrLocalSubject, iTempPassword :=  iTempPasswordLocal, iSetValue := iSetValueLocal, bReadWrite := bReadWriteLocal));
		//Once the Proxy has made the response, the memory is released by deleting the Proxy object
		__DELETE(ptrProxy);
	END_IF
	
	//We delete the local value of last password for security reasons. This way for every request the client must input a password
	iTempPasswordLocal := 0;
	//Once the WRITE request has been handled, we toggle the variable to avoid double requests
	bWriteRequest := FALSE;
END_IF

//If a READ or GET request is received
IF	bReadRequest
	THEN
	//We specify FALSE bReadWriteLocal because it is a GET request
	bReadWriteLocal := FALSE;
	//First we verify if the pointer to RealSubject to be used is a valid pointer
	IF ptrLocalSubject <> ptrNullRealSubject
		//If so,
		THEN
		//The Proxy object is created with all its parameters passed by the PLC_PRG local variables.
		//As there is no "Method Overloading" options is CODESYS (Or at least the constructor cannot be overloaded) the constructor must receive and handle all the possible scenarios
		// .ptrToRealSubject is a Pointer to the RealSubject instance that we try to conncect to
		// .iTempPassword is the password passed by the client in the visualization element
		// .iSetValue is the value passed by the client to be SET on the RealSubject. If the request is a GET request, this value has no effect on the output of the connection.
		// .bReadWrite is the boolean value that specifies if the request is a SET or GET request
		ptrProxy := __NEW(Proxy(ptrToRealSubject := ptrLocalSubject, iTempPassword :=  iTempPasswordLocal, iSetValue := 0, bReadWrite := bReadWriteLocal));
		//Once the Proxy has made the response, the memory is released by deleting the Proxy object
		__DELETE(ptrProxy);	
	END_IF
	
	//We delete the local value of last password for security reasons. This way for every request the client must input a password
	iTempPasswordLocal := 0;
	//Once the READ request has been handled, we toggle the variable to avoid double requests
	bReadRequest := FALSE;
END_IF